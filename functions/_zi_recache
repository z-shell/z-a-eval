# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Zi recache subcommand tab completion

builtin setopt local_options

typeset -a commands
commands=(
  recache:'Regenerate eval cache'
)

_arguments -C \
  '1: :->command'\
  '*: :->argument' && ret=0

case $state in
  command)
      _describe -t commands 'eval annex command' commands && ret=0
      ;;
  argument)
    case $words[2] in
      recache)
        # Plugins
        typeset -a plugins
        plugins=( "${ZI[PLUGINS_DIR]}"/**/evalcache.zsh(N:h:t) )
        plugins=( "${plugins[@]//---//}" )
        plugins=( "${plugins[@]:#_local/zi}" )
        plugins=( "${plugins[@]:#custom}" )
        # Snippets
        local -a snippets snippets_alreadyld
        local sni

        snippets=( "${ZI[SNIPPETS_DIR]}"/**/evalcache.zsh(N:h) )
        snippets=( ${snippets[@]#${ZI[SNIPPETS_DIR]}/} )
        snippets=( ${snippets[@]/(#b)(http|https|ftp|ftps|scp)--/${match[1]}://} )
        snippets=( ${snippets[@]/--//} )

        for sni ( ${snippets[@]} ) {
          if [[ -n ${ZI_SNIPPETS[$sni]} ]]; then
            snippets_alreadyld+=( $sni )
            snippets=( ${snippets[@]:#$sni} )
          fi
        }

        _alternative 'dsnippets:-- Downloaded Snippet --:compadd -a - snippets' \
          'lsnippets:-- Already Loaded Snippet --:compadd -a - snippets_alreadyld' \
          'plugins:-- Plugin --:compadd -a - plugins' && \
            ret=0
            ret=1
      ;;
      *)
        ret=1
      ;;
    esac
esac

return $ret
